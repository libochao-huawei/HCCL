set(DEFAULT_BUILD_TYPE "Release")

if (NOT CMAKE_BUILD_TYPE AND NOT CMAKE_CONFIGURATION_TYPES)
    set(CMAKE_BUILD_TYPE "${DEFAULT_BUILD_TYPE}" CACHE STRING "Choose the build type: Release/Debug" FORCE)
endif()

function(generate_stub_with_output_name STUB STUB_OUTPUT_NAME) 
    if(EXISTS ${DOWNLOAD_LIB_DIR}/lib${STUB_OUTPUT_NAME}.so) 
        add_library(${STUB} SHARED IMPORTED GLOBAL) 
        set_target_properties(${STUB} PROPERTIES 
            IMPORTED_LOCATION "${DOWNLOAD_LIB_DIR}/lib${STUB_OUTPUT_NAME}.so" 
            INTERFACE_LINK_OPTIONS "-Wl,-rpath-link=${DOWNLOAD_LIB_DIR}" 
        ) 
        message(STATUS "Imported library lib${STUB_OUTPUT_NAME}.so") 
    else() 
        string(FIND ${STUB_OUTPUT_NAME} "::" temp) 
        if (temp EQUAL "-1") 
            set(target_plain_name ${STUB_OUTPUT_NAME}) 
        else() 
            string(REPLACE "::" ";" temp_list ${STUB_OUTPUT_NAME}) 
            list(GET temp_list 1 target_plain_name) 
        endif() 


        if (NOT TARGET ${target_plain_name}_stub_tmp) 
            add_custom_command(OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/stub/${target_plain_name}.c 
                COMMAND ${CMAKE_COMMAND} -E make_directory ${CMAKE_CURRENT_BINARY_DIR}/stub 
                COMMAND ${CMAKE_COMMAND} -E touch ${CMAKE_CURRENT_BINARY_DIR}/stub/${target_plain_name}.c) 
            add_library(${target_plain_name}_stub_tmp SHARED ${CMAKE_CURRENT_BINARY_DIR}/stub/${target_plain_name}.c) 
            set_target_properties(${target_plain_name}_stub_tmp PROPERTIES 
                WINDOWS_EXPORT_ALL_SYMBOLS TRUE 
                LIBRARY_OUTPUT_NAME ${target_plain_name} 
                RUNTIME_OUTPUT_NAME ${target_plain_name} 
                LIBRARY_OUTPUT_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/stub 
                RUNTIME_OUTPUT_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/stub) 
        endif() 


        add_library(${STUB} SHARED IMPORTED GLOBAL) 
        if (UNIX) 
            set_target_properties(${STUB} PROPERTIES 
                IMPORTED_LOCATION "${CMAKE_CURRENT_BINARY_DIR}/stub/lib${target_plain_name}.so") 
        endif() 
        if (WIN32) 
            set_target_properties(${STUB} PROPERTIES 
                IMPORTED_LOCATION "${CMAKE_CURRENT_BINARY_DIR}/stub/${target_plain_name}.dll" 
                IMPORTED_IMPLIB "${CMAKE_CURRENT_BINARY_DIR}/stub/${target_plain_name}.lib") 
        endif() 
        add_dependencies(${STUB} ${target_plain_name}_stub_tmp) 


        message(STATUS "Stub library lib${STUB_OUTPUT_NAME}.so") 
    endif() 
endfunction() 


function(generate_stub STUB) 
    if(DEFINED STUB_OUTPUT_NAME_${STUB}) 
        set(STUB_OUTPUT_NAME ${STUB_OUTPUT_NAME_${STUB}}) 
    else() 
        set(STUB_OUTPUT_NAME ${STUB}) 
    endif() 


    generate_stub_with_output_name(${STUB} ${STUB_OUTPUT_NAME}) 


    if(DEFINED STUB_LINK_LIBRARIES_${STUB}) 
        foreach(LIB ${STUB_LINK_LIBRARIES_${STUB}}) 
            if(TARGET ${LIB}) 
                target_link_libraries(${STUB} INTERFACE ${LIB}) 
            endif() 
        endforeach() 
    endif() 
endfunction(generate_stub) 

if(AARCH_MODE)
    set(STUBS
        hcomm 
        ccl_kernel
        c_sec
        unified_dlog
        mmpa
    ) 
    foreach(STUB ${STUBS}) 
        if(NOT TARGET ${STUB}) 
            generate_stub(${STUB}) 
        endif() 
    endforeach()
elseif(KERNEL_MODE AND BUILD_OPEN_PROJECT)
    # Device aicpu 构建：8.5.0 CANN 下 devlib/device/libccl_kernel.so 不存在，需要生成桩库
    if(CUSTOM_ASCEND_CANN_PACKAGE_PATH)
        set(_hccl_devlib_dir ${CUSTOM_ASCEND_CANN_PACKAGE_PATH}/devlib/device)
    elseif(DEFINED ASCEND_CANN_PACKAGE_PATH)
        set(_hccl_devlib_dir ${ASCEND_CANN_PACKAGE_PATH}/devlib/device)
    endif()
    if(DEFINED _hccl_devlib_dir AND NOT EXISTS ${_hccl_devlib_dir}/libccl_kernel.so)
        if(NOT TARGET ccl_kernel)
            generate_stub(ccl_kernel)
        endif()
    endif()
endif()

if(CUSTOM_ASCEND_CANN_PACKAGE_PATH)
    set(ASCEND_CANN_PACKAGE_PATH  ${CUSTOM_ASCEND_CANN_PACKAGE_PATH})
elseif(DEFINED ENV{ASCEND_HOME_PATH})
    set(ASCEND_CANN_PACKAGE_PATH  $ENV{ASCEND_HOME_PATH})
elseif(DEFINED ENV{ASCEND_OPP_PATH})
    get_filename_component(ASCEND_CANN_PACKAGE_PATH "$ENV{ASCEND_OPP_PATH}/.." ABSOLUTE)
else()
    set(ASCEND_CANN_PACKAGE_PATH  "/usr/local/Ascend/ascend-toolkit/latest")
endif()

set(ASCEND_MOCKCPP_PACKAGE_PATH ${CMAKE_CURRENT_SOURCE_DIR})

# if (NOT EXISTS "${ASCEND_CANN_PACKAGE_PATH}")
#     message(FATAL_ERROR "${ASCEND_CANN_PACKAGE_PATH} does not exist, please install the cann package and set environment variables.")
# endif()

# if (NOT EXISTS "${THIRD_PARTY_NLOHMANN_PATH}")
#     message(FATAL_ERROR "${THIRD_PARTY_NLOHMANN_PATH} does not exist, please check the setting of THIRD_PARTY_NLOHMANN_PATH.")
# endif()

# ------------------------------------------------------------
# 前向兼容：探测 CANN 版本号，设置 HCCL_CANN_COMPAT_850
# ------------------------------------------------------------
set(HCCL_CANN_VERSION_NUM 0)
set(_hccl_cann_version_header "${ASCEND_CANN_PACKAGE_PATH}/include/version/cann_version.h")
message(STATUS "Checking CANN version header: ${_hccl_cann_version_header}")
if(EXISTS "${_hccl_cann_version_header}")
    file(STRINGS "${_hccl_cann_version_header}" _hccl_cann_ver_line
         REGEX "^#define[ \t]+CANN_VERSION_NUM[ \t]+")
    message(STATUS "CANN version line: [${_hccl_cann_ver_line}]")
    if(_hccl_cann_ver_line)
        # 形如: #define CANN_VERSION_NUM ((8 * 10000000) + (5 * 100000) + (0 * 1000))
        string(REGEX MATCH
               "\\(([0-9]+) \\* 10000000\\) \\+ \\(([0-9]+) \\* 100000\\) \\+ \\(([0-9]+) \\* 1000\\)"
               _ "${_hccl_cann_ver_line}")
        message(STATUS "Matched: m1=[${CMAKE_MATCH_1}] m2=[${CMAKE_MATCH_2}] m3=[${CMAKE_MATCH_3}]")
        if(NOT CMAKE_MATCH_1 STREQUAL "" AND NOT CMAKE_MATCH_2 STREQUAL "" AND NOT CMAKE_MATCH_3 STREQUAL "")
            math(EXPR HCCL_CANN_VERSION_NUM
                 "${CMAKE_MATCH_1} * 10000000 + ${CMAKE_MATCH_2} * 100000 + ${CMAKE_MATCH_3} * 1000")
        endif()
    endif()
endif()
message(STATUS "Detected CANN_VERSION_NUM = ${HCCL_CANN_VERSION_NUM}")
if(HCCL_CANN_VERSION_NUM GREATER 0 AND HCCL_CANN_VERSION_NUM LESS 90000000)
    set(HCCL_CANN_COMPAT_850 ON)
    message(STATUS "HCCL_CANN_COMPAT_850 = ON (forward-compat mode for CANN < 9.0.0)")
else()
    set(HCCL_CANN_COMPAT_850 OFF)
endif()
# 把版本号作为编译期宏，供 .cc/.h 内 `#if CANN_VERSION_NUM >= 90000000` 直接判断
if(HCCL_CANN_VERSION_NUM GREATER 0)
    add_compile_definitions(CANN_VERSION_NUM=${HCCL_CANN_VERSION_NUM})
endif()

#execute_process(COMMAND bash ${CMAKE_CURRENT_SOURCE_DIR}/cmake/scripts/check_version_compatiable.sh
#                             ${ASCEND_CANN_PACKAGE_PATH}
#                             hccl
#                             ${CMAKE_CURRENT_SOURCE_DIR}/version.info
#    RESULT_VARIABLE result
#    OUTPUT_STRIP_TRAILING_WHITESPACE
#    OUTPUT_VARIABLE CANN_VERSION
#    )

#if (result)
#    message(FATAL_ERROR "${CANN_VERSION}")
#else()
#     string(TOLOWER ${CANN_VERSION} CANN_VERSION)
#endif()

if (CMAKE_INSTALL_PREFIX STREQUAL /usr/local)
    set(CMAKE_INSTALL_PREFIX     "${CMAKE_CURRENT_SOURCE_DIR}/output"  CACHE STRING "path for install()" FORCE)
endif ()

set(HI_PYTHON                     "python3"                       CACHE   STRING   "python executor")

message(STATUS "config.cmake KERNEL_MODE=${KERNEL_MODE} BUILD_OPEN_PROJECT=${BUILD_OPEN_PROJECT}")
if(BUILD_OPEN_PROJECT AND KERNEL_MODE)
    set(PRODUCT_SIDE                  device)
else()
    set(PRODUCT_SIDE                  host)
endif()
set(INSTALL_LIBRARY_DIR hccl/lib64)
set(INSTALL_INCLUDE_DIR hccl/include)
set(INSTALL_AICPU_KERNEL_JSON_DIR hccl/built-in/data/op/aicpu)
set(INSTALL_DEVICE_TAR_DIR hccl/Ascend/aicpu)

if (ENABLE_TEST)
    set(CMAKE_SKIP_RPATH FALSE)
else ()
    set(CMAKE_SKIP_RPATH TRUE)
endif ()
